// NOTE: If built in an IDE, ensure that the latest version of Gradle is installed and that your IDE is configured to
// use it. The version of Gradle that is included in IDEs is usually an older version. On a Mac, using brew to install
// Gradle, Gradle is installed to /usr/local/opt/gradle/libexec
buildscript {
    ext {
        groovyVersion = '2.5.11' // '3.0.2'
        spockVersion = '1.3-groovy-2.5' // '2.0-M2-groovy-3.0'
        lombokVersion = '1.18.12'
    }
}

plugins {
    id 'java'
    id 'groovy'
    id 'org.springframework.boot' version '2.2.6.RELEASE'
    id 'io.spring.dependency-management' version '1.0.9.RELEASE'
}

group 'com.dbs.demo'
version '1.0-SNAPSHOT'

sourceCompatibility = 1.8

repositories {
    jcenter()
    mavenCentral()
}

bootJar {
    mainClassName = "com.dbs.demo.Application"
}

dependencies {

    // Lombok Annotations
    annotationProcessor group: 'org.projectlombok', name: 'lombok', version: lombokVersion
    compileOnly group: 'org.projectlombok', name: 'lombok', version: lombokVersion

    // Spring Dependencies
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-web'
    implementation group: 'javax.persistence', name: 'javax.persistence-api'
    implementation group: 'org.springframework.data', name: 'spring-data-jpa'
    implementation group: 'org.hibernate', name: 'hibernate-core', version: '5.4.14.Final'

    // DEVELOPER NOTE: Normally, we would only use H2 as a testCompile dependency. But this demo app doesn't have a
    // "real" database server anywhere to talk to, so we have to use H2 as this apps own private in-memory, throw-away
    // database.
    implementation group: 'com.h2database', name: 'h2', version: '1.4.200'

    // Spock/Test Dependencies
    // DEVELOPER NOTE: Do not add JUnit 5 dependencies or useJUnitPlatform(). Spock takes care of it. You can also
    // ignore the "WARNING: An illegal reflective access operation has occurred" messages. These are known warnings by
    // the Spock team, are harmless, and will be resolved in a future version.
    testImplementation platform(group: 'org.spockframework', name: 'spock-bom', version: spockVersion)
    testImplementation group: 'org.codehaus.groovy', name: 'groovy-all', version: groovyVersion
    testImplementation group: 'org.spockframework', name: 'spock-core', version: spockVersion
    testImplementation group: 'uk.co.jemos.podam', name: 'podam', version: '7.2.3.RELEASE'
    testImplementation group: 'com.athaydes', name: 'spock-reports', version: '1.7.1'

    // Spring/Test Dependencies
    testImplementation group: 'org.springframework', name: 'spring-test'
    testImplementation group: 'org.springframework.boot', name: 'spring-boot-test-autoconfigure'
    testImplementation group: 'org.spockframework', name: 'spock-spring', version: spockVersion

}
